cmake_minimum_required(VERSION 3.19)

set(SudokuLib_known_comps static shared)
set(SudokuLib_comp_static NO)
set(SudokuLib_comp_shared NO)
foreach (SudokuLib_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (SudokuLib_comp IN_LIST SudokuLib_known_comps)
        set(SudokuLib_comp_${SudokuLib_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "SudokuLib does not recognize component `${SudokuLib_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if (SudokuLib_comp_static AND SudokuLib_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
        "SudokuLib `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(SudokuLib_static_targets "${CMAKE_CURRENT_LIST_DIR}/SudokuLib-static-targets.cmake")
set(SudokuLib_shared_targets "${CMAKE_CURRENT_LIST_DIR}/SudokuLib-shared-targets.cmake")

macro(SudokuLib_load_targets type)
    if (NOT EXISTS "${SudokuLib_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "SudokuLib `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${SudokuLib_${type}_targets}")
endmacro()

if (SudokuLib_comp_static)
    SudokuLib_load_targets(static)
elseif (SudokuLib_comp_shared)
    SudokuLib_load_targets(shared)
elseif (DEFINED SudokuLib_SHARED_LIBS AND SudokuLib_SHARED_LIBS)
    SudokuLib_load_targets(shared)
elseif (DEFINED SudokuLib_SHARED_LIBS AND NOT SudokuLib_SHARED_LIBS)
    SudokuLib_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${SudokuLib_shared_targets}")
        SudokuLib_load_targets(shared)
    else ()
        SudokuLib_load_targets(static)
    endif ()
else ()
    if (EXISTS "${SudokuLib_static_targets}")
        SudokuLib_load_targets(static)
    else ()
        SudokuLib_load_targets(shared)
    endif ()
endif ()

